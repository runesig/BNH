OBJECT Codeunit 5231124 BNH Item
{
  OBJECT-PROPERTIES
  {
    Date=23.11.17;
    Time=10:16:42;
    Modified=Yes;
    Version List=Konv. BNH;
  }
  PROPERTIES
  {
    TableNo=5231124;
    OnRun=BEGIN
            BnHItemG.COPY( Rec );
            ProcessRecords;
            ShowResults;
          END;

  }
  CODE
  {
    VAR
      C_ConfirmProcess@1101103000 : TextConst 'ENU=Process %1 records?;NOR=Prosesser %1 poster?';
      BnHItemG@1101103001 : Record 5231124;
      C_Results@1101103003 : TextConst 'ENU=Results:\ \%1 OK\ \%2 Ignored\ \%3 Failed;NOR=Resultater:\ \%1 OK\ \%2 ignorert\ \%3 feil';
      RecordCountG@1101103004 : ARRAY [9] OF Integer;
      PDMProgressG@1101103002 : Codeunit 50009;
      C_Progress@1101103005 : TextConst 'ENU=Processing...;NOR=Prosesserer...';

    PROCEDURE ProcessRecords@1101103001();
    BEGIN
      WITH BnHItemG DO BEGIN
        RecordCountG[9] := COUNT;
        IF GUIALLOWED THEN
          IF NOT CONFIRM( C_ConfirmProcess, TRUE, RecordCountG[9] ) THEN
            EXIT;
        PDMProgressG.Start( C_Progress, RecordCountG[9] );
        IF FINDSET THEN
          REPEAT
            PDMProgressG.Increment( 1 );
            COMMIT;
            CLEARLASTERROR;
            CASE TRUE OF
              Status = Status::Ignore:
                ;
              CODEUNIT.RUN( CODEUNIT::"BNH Item Update", BnHItemG ):
                SetStatus( Status::OK, '' )
              ELSE
                SetStatus( Status::Error, GETLASTERRORTEXT );
            END;
            RecordCountG[ Status ] +=1;
          UNTIL NEXT = 0;
        PDMProgressG.Stop;
      END;
      COMMIT;
    END;

    PROCEDURE SetStatus@1101103000(StatusP@1101103000 : Option;MessageP@1101103001 : Text);
    BEGIN
      WITH BnHItemG DO BEGIN
        GET( "Primary Key" );
        CASE TRUE OF
          Status = Status::Ignore:
            ;
          StatusP = Status::OK:
            Status := StatusP;
          ELSE BEGIN
            Status := StatusP;
            Datetime := CURRENTDATETIME;
            "Update message" := COPYSTR( MessageP, 1, MAXSTRLEN( "Update message" ));
            MODIFY;
          END;
        END;
      END;
    END;

    PROCEDURE ShowResults@1101103003();
    BEGIN
      WITH BnHItemG DO
        IF GUIALLOWED THEN
          MESSAGE( C_Results, RecordCountG[ Status::OK ], RecordCountG[ Status::Ignore ], RecordCountG[ Status::Error ] );
    END;

    BEGIN
    END.
  }
}

